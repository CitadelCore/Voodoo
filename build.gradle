buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "net.sf.proguard:proguard-gradle:6.0.1"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:0.9.17"
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '2.0.3'
    id "org.jetbrains.kotlin.jvm" version "1.2.51"
    //TODO: move to buildscript and use kotlin version in properties file (or +)
    id "org.sonarqube" version "2.6.2"
    id 'idea'
}

println """
*******************************************
 You are building Voodoo Toolset ! 

 Output files will be in [subproject]/build/libs
*******************************************
"""

idea {
    module {
        excludeDirs += [file("run")]
    }
}

repositories {
    mavenCentral()
}

allprojects {
    apply plugin: "kotlin"
    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    compileKotlin {
        kotlinOptions {
            jvmTarget = "1.8"
            languageVersion = "1.2"
        }
    }
    kotlin {
        experimental {
            coroutines 'enable'
        }
    }

    repositories {
        mavenCentral()
        jcenter()
        maven { url = 'http://repo.maven.apache.org/maven2' }
        maven { url = 'http://jcenter.bintray.com' }
        maven { url = 'https://jitpack.io' }
    }
}

subprojects {
    apply plugin: "kotlin"
    apply plugin: 'org.jetbrains.dokka'
    group = group
    version = version

    idea {
        module {
            excludeDirs += [file("run")]
        }
    }

    if (name == 'Jankson') {
        sonarqube {
            skipProject = true
        }
    }
    if (name != "Jankson") {
        sourceSets {
            main.kotlin.srcDirs += "$buildDir/generated-src"
        }
//    version = "${project.major}.${project.minor}.${project.patch}"
        compileKotlin.doFirst {
            def folder = project.name != "voodoo" ? "voodoo/${project.name}" : "voodoo"
            def name = project.name.tokenize('/').last().capitalize().tokenize('-')*.capitalize().join('')
            def templateSrc = project.parent.file("template/kotlin/voodoo/")
            copy {
                from templateSrc
                into "$buildDir/generated-src/$folder"
                expand(
                        PACKAGE: folder.replace('/', '.').replace('-', '.'),
                        NAME: name,
                        MAJOR_VERSION: project.major,
                        MINOR_VERSION: project.minor,
                        PATCH_VERSION: project.patch,
                        BUILD_NUMBER: System.env.BUILD_NUMBER ?: -1,
                        BUILD: System.env.BUILD_NUMBER ?: "dev"
                )
            }
//            fileTree("$buildDir/generated-src/$folder").visit { FileVisitDetails details ->
//                println "# " +details.file.path
//                details.file.readLines().forEach {
//                    println it
//                }
//            }

        }
        if (System.env.BUILD_NUMBER) {
            version = System.env.BUILD_NUMBER
        }
    }

    dokka {
        outputFormat = 'gfm'
        outputDirectory = "${parent.projectDir}/docs/dokka"
    }
}

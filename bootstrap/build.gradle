import proguard.gradle.ProGuardTask

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "net.sf.proguard:proguard-gradle:6.0.1"
    }
}

plugins {
    id 'application'
}

apply plugin: 'com.github.johnrengelman.shadow'

mainClassName = "voodoo.BootstrapKt"
version = project.version

dependencies {
    compile project(':util')
}

shadowJar {
    classifier = "fat"
}

build.dependsOn(shadowJar)

def moduleName
def fileRegex

if(project.hasProperty('target')) {
    if (project.target == "voodoo") {
        moduleName = "voodoo"
        fileRegex = /^[Vv]oodoo(-.*)?\\.jar$/
        shadowJar.baseName = "bootstrap-voodoo"
    } else if (project.target == "multimc-installer") {
        moduleName = "multimc-installer"
        fileRegex = /^[Hh]ex(-.*)?\\.jar$/
        shadowJar.baseName = "bootstrap-multimc-installer"
    } else if (project.target == "archiver") {
        moduleName = "archiver"
        fileRegex = /^[Aa]rchiver(-.*)?\\.jar$/
        shadowJar.baseName = "bootstrap-archiver"
    } else {
        throw new InvalidUserDataException("invalid target property")
    }
}

compileKotlin.doFirst {
    copy {
        from "src/template/kotlin/voodoo/"
        into "$buildDir/generated-src/voodoo/"
        expand(
                JENKINS_URL: "https://ci.elytradev.com",
                JENKINS_JOB: "elytra/Voodoo/master",
                MODULE_NAME: moduleName,
                FILE_REGEX: fileRegex
        )
    }
}

task minify(type: ProGuardTask, dependsOn: 'shadowJar') {
    libraryjars "${System.getProperty('java.home')}/lib/rt.jar"
    libraryjars "${System.getProperty('java.home')}/lib/jce.jar"

    injars  shadowJar
    outjars "build/libs/$shadowJar.baseName${version != 'unspecified' ? "-$version" : ""}.jar"

    printmapping 'build/libs/out.map'

//    optimizations '!code/allocation/variable'
    optimizations '*'

    keepclasseswithmembers 'public class * { \
        public static void main(java.lang.String[]); \
    }'

    assumenosideeffects 'class kotlin.jvm.internal.Intrinsics { \
        static void checkParameterIsNotNull(java.lang.Object, java.lang.String); \
    }'

    // Jackson
    keepattributes '*Annotation*'
    keepattributes 'EnclosingMethod'
    keepattributes 'Signature'
    keepnames 'class com.fasterxml.jackson.** {  *; }'
    dontwarn 'com.fasterxml.jackson.databind.**'
    keep 'class org.codehaus.** { *; }'
    keepclassmembers 'public final enum org.codehaus.jackson.annotate.JsonAutoDetect$Visibility { \
       public static final org.codehaus.jackson.annotate.JsonAutoDetect$Visibility *; }'
    keepclassmembers 'enum * { \
        public static **[] values(); \
        public static ** valueOf(java.lang.String); \
    }'

    // Kotlin
    keep 'class org.jetbrains.annotations.** { *; }'
    keepclassmembers 'class ** { \
        @org.jetbrains.annotations.ReadOnly public *; \
    }'
    keep 'class kotlin.Metadata { *; }'

    //Kotlin Reflect
    keep 'class kotlin.reflect.** { *; }'

    // JNA
    dontwarn 'java.awt.*'
    keep 'class com.sun.jna.* { *; }'
    keepclassmembers 'class * extends com.sun.jna.* { public *; }'

    // apache commons compress
    dontwarn 'org.apache.commons.compress.compressors.**'
    dontwarn 'org.apache.commons.compress.archivers.**'

    // Logging
    keep 'class ch.qos.** { *; }'
    keep 'class org.slf4j.** { *; }'
    keepattributes '*Annotation*'
    dontwarn 'ch.qos.logback.core.net.*'

    // Voodoo
    keepattributes 'LineNumberTable'
    keep 'public class voodoo.** {\
        *;\
    }'

//    keepnames 'class * { *; }'
}

configure(minify) {
    group = 'build'
    description = 'Optimize JAR'
}

build.dependsOn(minify)
